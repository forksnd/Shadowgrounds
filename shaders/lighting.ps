//#define ENABLE_LIGHTING
//#define ENABLE_LIGHTMAP
//#define ENABLE_FAKE_LIGHT
//#define ENABLE_FOG
//#define TEXTURE_SOURCE 0

#define TEXSRC_NO_TEXTURE 0
#define TEXSRC_TEXTURE 1
#define TEXSRC_REFLECTION 2
#define TEXSRC_LOCAL_REFLECTION 3

struct PS_IN
{
    float4 diffuse   : COLOR0;
    float2 uv0       : TEXCOORD0;
    float2 uv1       : TEXCOORD1;
#if ENABLE_LIGHTING
    float3 lighting  : COLOR1;
    #if ENABLE_FAKE_LIGHT
        float4 uv2   : TEXCOORD2;
    #endif
#endif
#if TEXTURE_SOURCE == TEXSRC_REFLECTION
    float3 uv3       : TEXCOORD3;
#elif TEXTURE_SOURCE == TEXSRC_LOCAL_REFLECTION
    float4 uv3       : TEXCOORD3;
#endif
#if ENABLE_FOG
    float fogFactor  : FOG;
#endif
};

struct PS_OUT
{
    float4 rt0 : COLOR;
};

#if ENABLE_FOG
    // Fog parameters
    float3   uFogColor : register(c20);
#endif

#if TEXTURE_SOURCE != TEXSRC_NO_TEXTURE
    sampler2D samDiffuse : register(s0);
#endif

#if TEXTURE_SOURCE == TEXSRC_REFLECTION
    float3 uReflectionMult : register(c1);
    samplerCUBE samReflection : register(s3);
#elif TEXTURE_SOURCE == TEXSRC_LOCAL_REFLECTION
    float3 uReflectionMult : register(c1);
    sampler2D samReflection : register(s3);
#endif

#if ENABLE_LIGHTING
    #if ENABLE_LIGHTMAP
        float3 uLightmapMult : register(c0);
        sampler2D samLightmap : register(s1);
    #endif
    #if ENABLE_FAKE_LIGHT
        sampler2D samFakeLight : register(s2);
    #endif
#endif

PS_OUT main(PS_IN ps_in)
{
    PS_OUT ps_out = (PS_OUT)0;

    float4 color = ps_in.diffuse;

#if TEXTURE_SOURCE != TEXSRC_NO_TEXTURE
    color *= tex2D(samDiffuse, ps_in.uv0);
#endif
#if TEXTURE_SOURCE == TEXSRC_REFLECTION
    float3 reflection = texCUBE(samReflection, ps_in.uv3).rgb;
    color.rgb = saturate(reflection * uReflectionMult + color.rgb);
#elif TEXTURE_SOURCE == TEXSRC_LOCAL_REFLECTION
    float3 reflection = tex2Dproj(samReflection, ps_in.uv3).rgb;
    color.rgb = lerp(reflection, color.rgb, uReflectionMult);
#endif
#if ENABLE_LIGHTING
    float3 lighting = ps_in.lighting;
    #if ENABLE_LIGHTMAP
        float3 lightmap = tex2D(samLightmap, ps_in.uv1).rgb;
        lighting = saturate(uLightmapMult * lightmap + lighting);
    #endif
    #if ENABLE_FAKE_LIGHT
        float3 fakeLight = tex2Dproj(samFakeLight, ps_in.uv2).rgb;
        lighting = saturate(fakeLight + lighting);
    #endif
    color.rgb *= 2 * lighting;//Do we need to multiply by 2?????
#endif
#if ENABLE_FOG
    color.rgb = lerp(uFogColor, color.rgb, ps_in.fogFactor);
#endif

    ps_out.rt0 = color;

#if TEXTURE_SOURCE == 0
    //ps_out.rt0 = float4(1, 0, 0, 1);
#endif
#if TEXTURE_SOURCE == 2
    //ps_out.rt0 = float4(0, 1, 0, 1);
#endif
#if TEXTURE_SOURCE == 3
    //ps_out.rt0 = float4(0, 0, 1, 1);
#endif

    return ps_out;
}
